//=========================================================
//		Apex Roleplaying Engine
//		Copyright (c) 2022, Apex Group
//=========================================================
// This software is distributed under the MIT License:
// <http://opensource.org/licenses/MIT>
//=========================================================

// LibapxCommandRegionCreate
// ====================
// creates a region and sets it into the global data table
// regions are stored in format:
// "reg [player] [name]"
void LibapxCommandRegionCreate(int user, unitgroup u, string argv, int spawningCount, string bracketText) {
	region r;
	fixed radius;
	string name = StringWord(argv, 2);

	if( name == "" ) {
		LibapxPrintError(user, "Invalid region name");
		return;
	}

	radius = StringToFixed(StringWord(argv, 3));
	if( radius == 0.0 ) {
		radius = 2;
	}

	if( DataTableGetRegion( true, LibapxRegionGetName(name, user) ) ) {
		LibapxPrintError(user, "A region with that name already exists!");
		return;
	}

	r = RegionCircle( gv_LibapxPlayer[user].mousePosition, radius );

	DataTableSetRegion( true, LibapxRegionGetName(name, user), r );
	LibapxPrintOutput(user, "Region (circle) \"" + name + "\" has been created.");

	return;
}
void LibapxDefineRegionCreate(string commandName) {
	LibapxCommandsGuideEntrySet(
		gv_CommandsGuideEntryRegion, 
		commandName, 
		" This command creates a region with a given radius. <br> #create [name] [radius] <br> Examples: <br> #create bob 3 "
	);
}

// LibapxCommandRegionRemove
// ==================
// removes a region and any linking to other regions
// (for linking, see LibapxCommandRegionTeleport below)
void LibapxCommandRegionRemove(int user, unitgroup u, string argv, int spawningCount, string bracketText) {
	string name = LibapxRegionGetName(StringWord(argv, 2), user);
	region rgn = DataTableGetRegion( true, name );

	if( rgn == null ) {
		LibapxPrintError(user, "Region does not exist");
		return;
	}

	// first remove any linking data (for teleports) related to this region
	LibapxRegionRemoveLinks(name, user);

	// remove region
	DataTableValueRemove( true, name );

	// if region attached to unit, remove unit anchor data table entry
	if( DataTableValueExists(true, LibapxREGIONANCHORDATATABLEIDENTIFIER + " " + name) ) {
		DataTableValueRemove(true, LibapxREGIONANCHORDATATABLEIDENTIFIER + " " + name);
	}

	LibapxPrintOutput(user, "Region \"" + LibapxRegionGetPublicName(name) + "\" has been removed");

	return;
}
void LibapxDefineRegionRemove(string commandName) {
	LibapxCommandsGuideEntrySet(
		gv_CommandsGuideEntryRegion, 
		commandName, 
		" This command removes a region that matches the name given. <br> #remove [name]<br> Examples: <br> #remove bob "
	);
}
// LibapxCommandRegionRemoveall
// ======================
// removes all regions and links
void LibapxCommandRegionRemoveall(int user, unitgroup u, string argv, int spawningCount, string bracketText) {
	int index = 1;
	int removed = 0;
	string rgnIdentifier = LibapxRegionGetName("", user);
	string name; // full region data table name

	while( index <= DataTableValueCount(true) ) {
		if( StringContains(DataTableValueName(true, index), rgnIdentifier, c_stringBegin, c_stringNoCase) ) {
			name = DataTableValueName(true, index);
			LibapxRegionRemoveLinks( name, user );
			DataTableValueRemove(true, name);
			// if region attached to unit, remove unit anchor data table entry
			if( DataTableValueExists(true, LibapxREGIONANCHORDATATABLEIDENTIFIER + " " + name) ) {
				DataTableValueRemove(true, LibapxREGIONANCHORDATATABLEIDENTIFIER + " " + name);
			}

			removed += 1;
		}
		index += 1;
	}

	if(removed > 0) {
		LibapxPrintOutput(user, "All your regions have been removed");
	}
	else {
		LibapxPrintOutput(user, "You have no regions to remove");
	}

	return;
}
void LibapxDefineRegionRemoveall(string commandName) {
	LibapxCommandsGuideEntrySet(
		gv_CommandsGuideEntryRegion, 
		commandName, 
		" This command removes all regions you have. <br> #removeall<br> Examples: <br> #removeall "
	);
}

// LibapxCommandRegionList
// ======================
// lists a player's regions from the data table
// because region creation is not case sensitive,
// these will always be in lowercase
void LibapxCommandRegionList(int user, unitgroup u, string argv, int spawningCount, string bracketText) {
	int i = 1;
	string regionList = "";

	while( i < DataTableValueCount(true) ) {
		if( StringWord( DataTableValueName(true, i), 1 ) == LibapxREGIONDATATABLEIDENTIFIER &&
			StringToInt( StringWord( DataTableValueName(true, i), 2) ) == user ) {
			regionList = regionList + ", " + LibapxRegionGetPublicName(DataTableValueName(true, i));
		}
		i += 1;
	}

	if( regionList != "" ) {
		regionList = StringSub(regionList, 3, StringLength(regionList)); // get rid of leading ", "
		LibapxPrintOutput(user, "Your regions:\n" + regionList);
	}
	else {
		LibapxPrintOutput(user, "You have no regions");
	}

	return;
}
void LibapxDefineRegionList(string commandName) {
	LibapxCommandsGuideEntrySet(
		gv_CommandsGuideEntryRegion, 
		commandName, 
		" This command list all regions you have. <br> #list <br> Examples: <br> #list "
	);
}
// LibapxCommandRegionTeleport
// =========================
// creates a "waygate" between two regions, allowing (any) player
// to press a dialog button to teleport selected units between
// regions. (see engine/regions.galaxy to see how regions are linked,
// events/regionWaygates.galaxy to see how waygates function)
// (heavily modified from Cortex engine)
void LibapxCommandRegionTeleport(int user, unitgroup u, string argv, int spawningCount, string bracketText) {
	string rgn_1Name = LibapxRegionGetName(StringWord(argv, 2), user);
	string rgn_2Name = LibapxRegionGetName(StringWord(argv, 3), user);
	region rgn_1;
	region rgn_2;
	trigger t;

	if( !DataTableValueExists(true, rgn_1Name) || !DataTableValueExists(true, rgn_2Name) ) {
		LibapxPrintError(user, "One or more invalid regions were entered.");
		return;
	}

	rgn_1 = DataTableGetRegion( true, rgn_1Name );
	rgn_2 = DataTableGetRegion( true, rgn_2Name );

	// remove existing links for the regions
	LibapxRegionRemoveLinks(rgn_1Name, user);
	LibapxRegionRemoveLinks(rgn_2Name, user);

	// create link between these two regions
	LibapxLinkRegions(rgn_1Name, rgn_2Name);

	t = TriggerCreate("LibapxRegionEntered");
	TriggerAddEventUnitRegion( t, null, rgn_1, true );
	TriggerAddEventUnitRegion( t, null, rgn_1, false );
	TriggerAddEventUnitRegion( t, null, rgn_2, true );
	TriggerAddEventUnitRegion( t, null, rgn_2, false );

	LibapxPrintOutput(user, "Teleport created between regions " + LibapxRegionGetPublicName(rgn_1Name)
	                    + " and " + LibapxRegionGetPublicName(rgn_2Name));

	return;
}
void LibapxDefineRegionTeleport(string commandName) {
	LibapxCommandsGuideEntrySet(
		gv_CommandsGuideEntryRegion, 
		commandName, 
		" This command connects two regions together. Allowing units to teleport between the regions. <br> #teleport [region1] [region2] <br> Examples <br> #teleport a b <br> @spawn marine ; #create a ; Delay 3 ; #create b ; #teleport a b "
	);
}

// LibapxCommandRegionAttach
// =================
// attaches a region to a unit
void LibapxCommandRegionAttach(int user, unitgroup u, string argv, int spawningCount, string bracketText) {
	string name = StringWord(argv, 2);
	region rgn = null;
	unitgroup g = gv_LibapxPlayerParser[user][spawningCount].context;
	point offset;
	int waygateDialog; // waygate dialog for region

	name = LibapxRegionGetName(name, user);

	rgn = DataTableGetRegion(true, name);
	if(rgn == null) {
		LibapxPrintError(user, "No region with that name exists!");
		return;
	}

	offset = Point( StringToFixed(StringWord(argv, 3)),
					StringToFixed(StringWord(argv, 4)) );

	if(UnitGroupCount(g, c_unitCountAll) == 0) {	// no units selected -> unattach region
		RegionAttachToUnit(rgn, null, offset);
		DataTableValueRemove(true, LibapxREGIONANCHORDATATABLEIDENTIFIER + " " + name);
		LibapxPrintSuccess(user, "Region unattached successfully.");
	}
	else if(UnitGroupCount(g, c_unitCountAll) == 1) { // attach region to unit
		// if a unit has previously had region attached, remove unit custom value
		// set to track waygate dialogs (if it exists)
		if( DataTableValueExists(true, LibapxREGIONANCHORDATATABLEIDENTIFIER + " " + name) ) {
			UnitSetCustomValue(DataTableGetUnit(true, LibapxREGIONANCHORDATATABLEIDENTIFIER + " " + name), gv_LibapxUnitIndexWaygateDialog, 0);
		}


		// attach region to new unit
		RegionAttachToUnit(rgn, UnitGroupUnit(g, 1), offset);
		DataTableSetUnit(true, LibapxREGIONANCHORDATATABLEIDENTIFIER + " " + name, UnitGroupUnit(g, 1));

		// if region has assocated waygate dialog, attach it to the unit as well
		if( DataTableValueExists(true, LibapxWAYGATEDIALOGDATATABLEIDENTIFIER + " " + name) ) {
			waygateDialog = DataTableGetDialog(true, LibapxWAYGATEDIALOGDATATABLEIDENTIFIER + " " + name);
			DialogSetPositionRelativeToUnit(waygateDialog, UnitGroupUnit(g, 1), "RefOrigin", -60, 0);
			UnitSetCustomValue(UnitGroupUnit(g, 1), gv_LibapxUnitIndexWaygateDialog, IntToFixed(waygateDialog));

			// if a static anchor for waygate exists, remove it
			if( DataTableValueExists(true, LibapxWAYGATEANCHORDATATABLEIDENTIFIER + " " + name) ) {
				UnitRemove(DataTableGetUnit(true, LibapxWAYGATEANCHORDATATABLEIDENTIFIER + " " + name));
				DataTableValueRemove(true, LibapxWAYGATEANCHORDATATABLEIDENTIFIER + " " + name);
			}
		}

		LibapxPrintSuccess(user, "Region attached successfully.");
	}
	else {
		LibapxPrintError(user, "You can't attach the same region to multiple units!");
	}

	return;
}
void LibapxDefineRegionAttach(string commandName) {
	LibapxCommandsGuideEntrySet(
		gv_CommandsGuideEntryRegion, 
		commandName, 
		" This command attaches a region to a unit. <br> #attach [regionname] <br> Examples <br> #attach a <br> @spawn marine;#create a;#attach a "
	);
}