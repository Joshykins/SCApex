//=========================================================
//		Apex Roleplaying Engine
//		Copyright (c) 2019, Apex Group
//=========================================================
// This software is distributed under the MIT License:
// <http://opensource.org/licenses/MIT>
//=========================================================

//Destroys a region, (perma removes actors, useful for stain removal)
// =======================
void libapx_command_destroy(int user, unitgroup u, string argv, int spawningCount, string bracketText) {
	string paramRadius = StringWord(argv, 2 );
	fixed radius;
	if( libapx_player[user].permission_level < libapx_PERMISSIONS_LEVEL_MOD ) {
		libapx_print_error(user, "Only moderators and admins can use !destroy");
		return;
	}

	if(paramRadius == "") {
		radius = 5.0;
	}


	else {
		radius = StringToFixed(paramRadius);
		if(radius == 0.0) {
			radius = 5.0;
		}
	}


	libNtve_gf_SendActorMessageToGameRegion( (RegionCircle( libapx_player[user].mouse_position , radius)), "Destroy");


}
void libapx_define_destroy(string command_name) {
	commands_guide_entry_set(
		g_commands_guide_entry_admin, 
		command_name, 
		" This command destroys all actors and units within a given radius. It permanently removing these actors, never use it unless there is a stain you can't remove with @removearea. <br> This is an admin command so you must be a moderator or an admin to use it. <br> !destroy [radius] <br> Examples: <br> !destroy 3 "
	);
}

// libapx_command_ban
// ====================
// bans a player (removes all player's units from map too)
void libapx_command_ban(int user, unitgroup u, string argv, int spawningCount, string bracketText) {
	int target_player = libapx_color_to_player( StringWord(argv, 2) ); //GET PLAYER COLOR FROM STRING.
	unitgroup g;

	if(StringCase(StringWord(argv, 2), c_stringNoCase) == "me") {
		libapx_print_text(PlayerGroupAll(), libapx_player_name_colored(target_player) + StringToText(" has been kicked by himself, what a genius!"));
		GameOver( target_player, c_gameOverVictory, true, true );


		// remove all of a player's units
		g = UnitGroup( null, target_player, RegionEntireMap(), UnitFilter(0, 0, 0, 0), 0 );
		UnitGroupLoopBegin(g);
		while( !UnitGroupLoopDone() ) {
			UnitRemove( UnitGroupLoopCurrent() );
			UnitGroupLoopStep();
		}
		UnitGroupLoopEnd();
		return;

	}
	// check if player is valid
	if( target_player == -1 || PlayerType(target_player) != c_playerTypeUser ) {
		libapx_print_error(user, "Invalid player");
		return;
	}

	if( libapx_player[user].permission_level < libapx_PERMISSIONS_LEVEL_MOD ) {
		libapx_print_error(user, "Only moderators and admins can use \\kick");
		return;
	}
	if( libapx_player[user].permission_level <= libapx_player[target_player].permission_level ) {
		libapx_print_error(user, "You cannot kick someone of equal or greater rank than you");
		return;
	}

	GameOver( target_player, c_gameOverDefeat, false, true );


	// remove all of a player's units
	g = UnitGroup( null, target_player, RegionEntireMap(), UnitFilter(0, 0, 0, 0), 0 );
	UnitGroupLoopBegin(g);
	while( !UnitGroupLoopDone() ) {
		UnitRemove( UnitGroupLoopCurrent() );
		UnitGroupLoopStep();
	}
	UnitGroupLoopEnd();

	libapx_print_text(PlayerGroupAll(), libapx_player_name_colored(target_player) + StringToText(" has been kicked by ") + libapx_player_name_colored(user));

	return;
}
void libapx_define_ban(string command_name) {
	commands_guide_entry_set(
		g_commands_guide_entry_admin, 
		command_name, 
		" This command kicks any player from your lobby. <br> This is an admin command so you must be a moderator or an admin to use it. <br> !ban [playercolor] <br> Examples: <br> !kick purple "
	);
}

// libapx_command_disable
// ====================
// prevents player from using commands
// all chat instead routed directly to OOC
void libapx_command_disable(int user, unitgroup u, string argv, int spawningCount, string bracketText) {
	int target_player = libapx_color_to_player( StringWord(argv, 2) ); //GET PLAYER COLOR FROM STRING.

	// check if player is valid
	if( target_player == -1 || PlayerType(target_player) != c_playerTypeUser ) {
		libapx_print_error(user, "Invalid player");
		return;
	}

	if( libapx_player[user].permission_level < libapx_PERMISSIONS_LEVEL_MOD ) {
		libapx_print_error(user, "Only moderators and admins can use \\disable");
		return;
	}
	if( libapx_player[user].permission_level <= libapx_player[target_player].permission_level ) {
		libapx_print_error(user, "You cannot disable/enable someone of equal or greater rank than you");
		return;
	}

	libapx_player[target_player].disabled = true;
	libapx_print_text(PlayerGroupAll(), libapx_player_name_colored(target_player) + StringToText(" has been disabled by ") + libapx_player_name_colored(user));

	return;
}
void libapx_define_disable(string command_name) {
	commands_guide_entry_set(
		g_commands_guide_entry_admin, 
		command_name, 
		" This command disables the target player in your lobby. This means they can't use commands anymore until they are enabled. <br> This is an admin command so you must be a moderator or an admin to use it. <br> !disable [playercolor] <br> Examples: <br> !disable purple "
	);
}
// libapx_command_enable
// ====================
// "enables" a player; allows commands to be used
void libapx_command_enable(int user, unitgroup u, string argv, int spawningCount, string bracketText) {
	int target_player = libapx_color_to_player( StringWord(argv, 2) ); //GET PLAYER COLOR FROM STRING.

	// check if player is valid
	if( target_player == -1 || PlayerType(target_player) != c_playerTypeUser ) {
		libapx_print_error(user, "Invalid player");
		return;
	}

	if( libapx_player[user].permission_level < libapx_PERMISSIONS_LEVEL_MOD ) {
		libapx_print_error(user, "Only moderators and admins can use \\enable");
		return;
	}
	if( libapx_player[user].permission_level <= libapx_player[target_player].permission_level ) {
		libapx_print_error(user, "You cannot disable/enable someone of equal or greater rank than you");
		return;
	}

	libapx_player[target_player].disabled = false;
	libapx_print_text(PlayerGroupAll(), libapx_player_name_colored(target_player) + StringToText(" has been enabled by ") + libapx_player_name_colored(user));

	return;
}
void libapx_define_enable(string command_name) {
	commands_guide_entry_set(
		g_commands_guide_entry_admin, 
		command_name, 
		" This command enables the target player in your lobby. This means they can use commands now if they were disable beforehand. <br> This is an admin command so you must be a moderator or an admin to use it. <br> !enable [playercolor] <br> Examples: <br> !enable violet "
	);
}
// libapx_command_mute
// ====================
// prevents player's messages appearing in chat
void libapx_command_mute(int user, unitgroup u, string argv, int spawningCount, string bracketText) {
	int target_player = libapx_color_to_player( StringWord(argv, 2) ); //GET PLAYER COLOR FROM STRING.

	// check if player is valid
	if( target_player == -1 || PlayerType(target_player) != c_playerTypeUser ) {
		libapx_print_error(user, "Invalid player");
		return;
	}

	if( libapx_player[user].permission_level < libapx_PERMISSIONS_LEVEL_MOD ) {
		libapx_print_error(user, "Only moderators and admins can use \\mute");
		return;
	}
	if( libapx_player[user].permission_level <= libapx_player[target_player].permission_level ) {
		libapx_print_error(user, "You cannot mute/unmute someone of equal or greater rank than you");
		return;
	}

	libapx_player[target_player].muted = true;
	libapx_print_text(PlayerGroupAll(), libapx_player_name_colored(target_player) + StringToText(" has been muted by ") + libapx_player_name_colored(user));

	return;
}
void libapx_define_mute(string command_name) {
	commands_guide_entry_set(
		g_commands_guide_entry_admin, 
		command_name, 
		" This command mutes the target player in your lobby. This means their messages no longer appear in the chat. It can be undone with !unmute <br> This is an admin command so you must be a moderator or an admin to use it. <br> !mute [playercolor] <br> Examples: <br> !mute lightgrey "
	);
}

// libapx_command_unmute
// ====================
// unmutes a player (messages can appear in chat)
void libapx_command_unmute(int user, unitgroup u, string argv, int spawningCount, string bracketText) {
	int target_player = libapx_color_to_player( StringWord(argv, 2) ); //GET PLAYER COLOR FROM STRING.

	// check if player is valid
	if( target_player == -1 || PlayerType(target_player) != c_playerTypeUser ) {
		libapx_print_error(user, "Invalid player");
		return;
	}

	if( libapx_player[user].permission_level < libapx_PERMISSIONS_LEVEL_MOD ) {
		libapx_print_error(user, "Only moderators and admins can use \\unmute");
		return;
	}
	if( libapx_player[user].permission_level <= libapx_player[target_player].permission_level ) {
		libapx_print_error(user, "You cannot mute/unmute someone of equal or greater rank than you");
		return;
	}

	libapx_player[target_player].muted = false;
	libapx_print_text(PlayerGroupAll(), libapx_player_name_colored(target_player) + StringToText(" has been unmuted by ") + libapx_player_name_colored(user));

	return;
}
void libapx_define_unmute(string command_name) {
	commands_guide_entry_set(
		g_commands_guide_entry_admin, 
		command_name, 
		" This command unmutes the target player in your lobby. This means if they were muted their messages can now display in the chat. <br> This is an admin command so you must be a moderator or an admin to use it. <br> !unmute [playercolor] <br> Examples: <br> !unmute blue "
	);
}

// libapx_command_promote
// ================
// promotes a player to moderator status
void libapx_command_promote(int user, unitgroup u, string argv, int spawningCount, string bracketText) {
	int target_player = libapx_color_to_player( StringWord(argv, 2) ); //GET PLAYER COLOR FROM STRING.

	// check if player is valid
	if( target_player == -1 || PlayerType(target_player) != c_playerTypeUser ||
		target_player == user || libapx_player[target_player].permission_level == libapx_PERMISSIONS_LEVEL_ADMIN) {
		libapx_print_error(user, "Invalid player");
		return;
	}

	if( libapx_player[user].permission_level < libapx_PERMISSIONS_LEVEL_ADMIN ) {
		libapx_print_error(user, "Only admins can promote/demote moderators");
		return;
	}

	libapx_player[target_player].permission_level = libapx_PERMISSIONS_LEVEL_MOD;

	libapx_print_text(PlayerGroupAll(), libapx_player_name_colored(target_player) + StringToText(" has been promoted to moderator by ") + libapx_player_name_colored(user));

	return;
}
void libapx_define_promote(string command_name) {
	commands_guide_entry_set(
		g_commands_guide_entry_admin, 
		command_name, 
		"  This command promotes a player to a moderator. This means they can now use administration commands. <br> This is an admin-only command so you must be an admin to use it. <br> !promote [playercolor] <br> Examples: <br> !promote lightgreen "
	);
}

// libapx_command_demote
// ================
// sets a player to default status
void libapx_command_demote(int user, unitgroup u, string argv, int spawningCount, string bracketText) {
	int target_player = libapx_color_to_player( StringWord(argv, 2) ); //GET PLAYER COLOR FROM STRING.

	// check if player is valid
	if( target_player == -1 || PlayerType(target_player) != c_playerTypeUser ||
		target_player == user || libapx_player[target_player].permission_level == libapx_PERMISSIONS_LEVEL_ADMIN) {
		libapx_print_error(user, "Invalid player");
		return;
	}

	if( libapx_player[user].permission_level < libapx_PERMISSIONS_LEVEL_ADMIN ) {
		libapx_print_error(user, "Only admins can promote/demote moderators");
		return;
	}

	libapx_player[target_player].permission_level = libapx_PERMISSIONS_LEVEL_DEFAULT;

	libapx_print_text(PlayerGroupAll(), libapx_player_name_colored(target_player) + StringToText(" has been demoted by ") + libapx_player_name_colored(user));

	return;
}
void libapx_define_demote(string command_name) {
	commands_guide_entry_set(
		g_commands_guide_entry_admin, 
		command_name, 
		"  This command demote a player to from a moderator to a normal user. This means they can no longer use the administration commands. <br> This is an admin-only command so you must be an admin to use it. <br> !demote [playercolor] <br> Examples: <br> !demote pink "
	);
}


// libapx_command_skybox
// ==================
// Changes the map's skybox
void libapx_command_skybox(int user, unitgroup u, string argv, int spawningCount, string bracketText) {
	string model = StringWord(argv, 2);
	fixed animation_speed = StringToFixed(StringWord(argv, 3));

	if (StringWord(argv, 3) == "") {
		animation_speed = 1;
	}
	
	if( libapx_player[user].permission_level < libapx_PERMISSIONS_LEVEL_MOD  ) {
		libapx_print_error(user, "Only moderators or admins can change the skybox");
		return;
	}

	model = libapx_catalog_get(c_gameCatalogModel, model);
	if(model == null) {
		libapx_print_output(user, "<c val=\"ff0000\">Unable to find model</c>: No such model exists.");
		return;
	}

	GameSetBackground(c_backgroundFixed, model, animation_speed);
}
void libapx_define_skybox(string command_name) {
	commands_guide_entry_set(
		g_commands_guide_entry_admin, 
		command_name, 
		" This command changes the maps skybox model. <br> This is an admin command so you must be a moderator or an admin to use it. <br> !skybox [model] [animationspeed] <br> Examples: <br> !skybox KorhalFallingSky 1 ");
}

// libapx_command_light
// ==================
// Changes the map's lighting over a specified period of time
// if no light is specified, defaults to map terrain's lighting
void libapx_command_light(int user, unitgroup u, string argv, int spawningCount, string bracketText) {
	string light = StringWord(libapx_player[user].chat_msg, 2);
	fixed blend_time = StringToFixed(StringWord(libapx_player[user].chat_msg, 3));

	if( libapx_player[user].permission_level < libapx_PERMISSIONS_LEVEL_MOD  ) {
		libapx_print_error(user, "Only moderators or admins can change the light");
		return;
	}

	if(light == "") { // get default lighting for terrain set
		light = CatalogFieldValueGet(c_gameCatalogTerrain,  GameTerrainSet(), "Lighting", user);
	}

	light = libapx_catalog_get(c_gameCatalogLight, light);
	if(light == null) {
		libapx_print_error(user, "<c val=\"ff0000\">Unable to change lighting</c>: No such light exists.");
		return;
	}

	if(StringWord(libapx_player[user].chat_msg, 3) == "") {
		blend_time = 2;
	}

	GameSetLighting(light, blend_time);

	return;
}
void libapx_define_light(string command_name) {
	commands_guide_entry_set(
		g_commands_guide_entry_admin, 
		command_name, 
		" This command changes the maps lighting. If no light is specific it'll reset the light to the maps default light. <br> This is an admin command so you must be a moderator or an admin to use it. <br> !light [lightname] [duration] <br> Examples: <br> !light tassadar 5 <br> !light hyperion 1 ");
}

// libapx_command_remove_player
// ===================
// removes all of a specific players units from the map
void libapx_command_remove_player(int user, unitgroup u, string argv, int spawningCount, string bracketText) {
	int target_player = libapx_color_to_player( StringCase( StringWord(libapx_player[user].chat_msg, 2), false ) ); //GET PLAYER COLOR FROM STRING.
	unitgroup g;

	if( target_player == -1 ) {
		libapx_print_error(user, "Invalid player");
		return;
	}

	// if target_player is not yourself or a non-user, do permissions check
	if ( target_player != user && PlayerType(target_player) == c_playerTypeUser ) {
		if( libapx_player[user].permission_level < libapx_PERMISSIONS_LEVEL_MOD &&
			libapx_player[user].permission_level <= libapx_player[target_player].permission_level ) {
			libapx_print_error(user, "You cannot remove someone of equal or greater rank than you (while not a moderator or admin)");
			return;
		}
	}

	// remove all of target player's units
	g = UnitGroup( null, target_player, RegionEntireMap(), UnitFilter(0, 0, 0, 0), 0 );
	UnitGroupLoopBegin(g);
	while( !UnitGroupLoopDone() ) {
		UnitRemove( UnitGroupLoopCurrent() );
		UnitGroupLoopStep();
	}
	UnitGroupLoopEnd();

	libapx_print_text(PlayerGroupAll(), libapx_player_name_colored(target_player) + StringToText(" has been removed by ") + libapx_player_name_colored(user));

	return;
}

void libapx_define_remove_player(string command_name) {
	commands_guide_entry_set(
		g_commands_guide_entry_admin, 
		command_name, 
		" This command removes all of a targeted players units. <br> This is an admin command so you must be a moderator or an admin to use it. <br> !remove [playercolor] <br> Examples: <br> !remove teal ");
}

// libapx_command_fog
// ===================
// changes the fog of a map
void libapx_command_fog(int user, unitgroup u, string argv, int spawningCount, string bracketText) {
	string state = StringWord(argv, 2 );
	string paramOne;
	string paramTwo;
	string paramThree;
	fixed red;
	fixed green;
	fixed blue;
	if( libapx_player[user].permission_level < libapx_PERMISSIONS_LEVEL_MOD  ) {
		libapx_print_error(user, "Only moderators or admins can change the fog");
		return;
	}
	paramOne = StringWord(argv, 3);
	paramTwo = StringWord(argv, 4);
	paramThree = StringWord(argv, 5);

	if(state == "off")
	{ 
		FogSetEnabled(false);
	}
	else if(state == "on")
	{
		FogSetEnabled(true);
	}
	else if(state == "color")
	{
		red = libapx_boundsFixed(StringToFixed(paramOne),0,255) * 100 / 255;
		green = libapx_boundsFixed(StringToFixed(paramTwo),0,255) * 100 / 255;
		blue = libapx_boundsFixed(StringToFixed(paramThree),0,255) * 100 / 255;
		FogSetColor(Color(red, green, blue));
	}
	else if(state == "density")
	{
		FogSetDensity(StringToFixed(paramOne) / 100);
	}
	else if(state == "height")
	{
		FogSetStartHeight(StringToFixed(paramOne));
	}
	else if(state == "falloff")
	{
		FogSetFallOff(StringToFixed(paramOne) / 100);
	}
	else
	{
		libapx_print_output( user, "Usage: !fog {on|off|color|density|height|falloff} [value]");
	}
	return;
}

void libapx_define_fog(string command_name) {
	commands_guide_entry_set(
		g_commands_guide_entry_admin, 
		command_name, 
		" This command changes the fog of a map. Doing this is a permanent change and cannon't be undone easily. <br> This is an admin command so you must be a moderator or an admin to use it. <br> !fog [state] [parameters... <br> Examples: <br> !fog on <br> !fog off <br> !fog color 255 0 0 <br> !fog density 2 <br> !fog height 12 <br> !fog falloff 50");
}
