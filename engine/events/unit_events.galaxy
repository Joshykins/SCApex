//=========================================================
//		Apex Roleplaying Engine
//		Copyright (c) 2022, Apex Group
//=========================================================
// This software is distributed under the MIT License:
// <http://opensource.org/licenses/MIT>
//=========================================================

// LibapxUnitDeathCleanup
// =======================
// whenever unit is killed or is removed, must cleanup unit information:
// -> clean data: must remove unit's cache from the data table
//    (to conserve data table memory usage)
//    along with custom name/name color
// -> if unit has region waygate dialog attached, must anchor the waygate
//    dialog to position where unit died
bool LibapxUnitDeathCleanup(bool testConds, bool runActions) {
	int waygateDialog = FixedToInt( UnitGetCustomValue(EventUnit(), gv_LibapxUnitIndexWaygateDialog) );
	string rgnName;
	int i;

	// remove data table data (see unit.galaxy)
	LibapxUnitDataRemove(EventUnit());

	// if waygate dialog exists, anchor to position where unit died
	if( waygateDialog > 0 ) {
		rgnName = DataTableGetString(true, LibapxWAYGATEDIALOGREGDATATABLEIDENTIFIER + " " + IntToString(waygateDialog));
		// anchor dialog to position where unit died with a new unit
		UnitCreate(1, LibapxWAYGATEDIALOGANCHORUNIT, c_unitCreateIgnorePlacement, 0, UnitGetPosition(EventUnit()), 270.0);
		DataTableSetUnit(true, LibapxWAYGATEANCHORDATATABLEIDENTIFIER + " " + rgnName, UnitLastCreated());

		//ActorSend(libNtve_gf_MainActorofUnit(UnitLastCreated()), "SetOpacity {0.1}"); // set opacity to hide
		// hide the beacons for all players
		for( i = 1; i < gv_LibapxMaxPlayers; i += 1 ) {
			ActorSend(libNtve_gf_MainActorofUnit(UnitLastCreated()), "SetFilterPlayers " + IntToString(i));
		}
		UnitSetCustomValue(UnitLastCreated(), gv_LibapxUnitIndexWaygateAnchor, 1.0);

		DialogSetPositionRelativeToUnit(waygateDialog, UnitLastCreated(), "RefOverhead", -60, 0);
	}

	return true;
}
